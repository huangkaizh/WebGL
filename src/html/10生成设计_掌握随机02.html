<script src="../js/gl_utils.js"></script>

<body bgcolor="gray">
  <canvas id="canvas1" width="720" height="720"> </canvas>
</body>

<script id="vs" type="x-shader/x-vertex">
  attribute vec3 aPosition;
  varying   vec3 vPosition;
  void main() {
     gl_Position = vec4(aPosition, 1.0);
     vPosition = aPosition;
  }
</script>

<script id="fs" type="x-shader/x-fragment">
  // Author @patriciogv - 2015
  // Title: Ikeda Data Stream

  #ifdef GL_ES
  precision mediump float;
  #endif

  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;

  float random (in float x) {
      return fract(sin(x)*1e4);
  }

  float random (in vec2 st) {
      return fract(sin(dot(st.xy, vec2(12.9898,78.233)))* 43758.5453123);
  }

  float pattern(vec2 st, vec2 v, float t) {
      vec2 p = floor(st+v);
      return step(t, random(100.+p*.000001)+random(p.x)*0.5 );
  }

  void main() {
      vec2 st = gl_FragCoord.xy/u_resolution.xy;
      st.x *= u_resolution.x/u_resolution.y;

      vec2 grid = vec2(100.0,50.);
      st *= grid;

      vec2 ipos = floor(st);  // integer
      vec2 fpos = fract(st);  // fraction

      vec2 vel = vec2(u_time*2.*max(grid.x,grid.y)); // time
      vel *= vec2(-1.,0.0) * random(1.0+ipos.y); // direction

      // Assign a random value base on the integer coord
      vec2 offset = vec2(0.1,0.);

      vec3 color = vec3(0.);
      color.r = pattern(st+offset,vel,0.5+u_mouse.x/u_resolution.x);
      color.g = pattern(st,vel,0.5+u_mouse.x/u_resolution.x);
      color.b = pattern(st-offset,vel,0.5+u_mouse.x/u_resolution.x);

      // Margins
      color *= step(0.2,fpos.y);

      gl_FragColor = vec4(1.0-color,1.0);
  }
</script>

<script>
  start_gl(
    "canvas1",
    getStringFromDOMElement("vs"),
    getStringFromDOMElement("fs")
  );
</script>

<style>
  body {
    margin: 0;
  }
</style>
